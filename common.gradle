allprojects { subproject ->

    group = 'com.company.example'
    version = '1.0.0-SNAPSHOT'

    repositories {
        jcenter()
        maven { url "https://repo.adobe.com/nexus/content/groups/public" }
        maven { url "https://repo1.maven.org/maven2" }
        maven { url  "https://dl.bintray.com/neva-dev/maven-public" }
    }

    plugins.withId 'com.cognifide.aem.base', {

        // Common AEM configuration (CRX packages, deployment etc)

        aem {
            config {

                // https://github.com/Cognifide/gradle-aem-plugin#additional

            }
        }

    }

    plugins.withId 'com.cognifide.aem.bundle', {

        // Unified bundle configuration

        aem {
            bundle {
                attribute 'Bundle-Category', 'example'
                attribute 'Bundle-Vendor', 'Company'
            }
        }

    }

    plugins.withId 'com.moowork.node', {
        node {
            version = '6.11.1'
            yarnVersion = '0.27.5'
            download = true
        }
    }


    plugins.withId 'org.jetbrains.kotlin.jvm', {
        def compileOptions = {
            kotlinOptions.jvmTarget = "1.8"
        }

        compileKotlin compileOptions
        compileTestKotlin compileOptions
    }

    plugins.withId 'java', {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        def compileOptions = {
            it.options.encoding = "UTF-8"
        }

        compileJava compileOptions
        compileTestJava compileOptions

        test {
            useJUnitPlatform()
            failFast = true
        }

        dependencies {

            // AEM runtime dependencies

            compileOnly group: 'org.osgi', name: 'osgi.cmpn', version: '6.0.0'
            compileOnly group: 'org.osgi', name: 'org.osgi.core', version: '6.0.0'

            compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.5'
            compileOnly group: 'javax.jcr', name: 'jcr', version: '2.0'
            compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
            compileOnly group: 'org.apache.geronimo.specs', name: 'geronimo-atinject_1.0_spec', version: '1.0'
            // TODO fix it (no such package in 1.x version on instance)
            compileOnly group: 'org.apache.geronimo.specs', name: 'geronimo-annotation_1.0_spec', version: '1.1.1'

            compileOnly group: 'org.apache.sling', name: 'org.apache.sling.api', version: '2.16.4'
            compileOnly group: 'org.apache.sling', name: 'org.apache.sling.jcr.api', version: '2.4.0'
            compileOnly group: 'org.apache.sling', name: 'org.apache.sling.models.api', version: '1.3.6'
            compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
            compileOnly group: 'com.adobe.aem', name: 'uber-jar', version: '6.4.0', classifier: 'obfuscated-apis'
            compileOnly group: 'joda-time', name: 'joda-time', version: '2.9.1'
            compileOnly group: 'org.jetbrains', name: 'annotations', version: '13.0'

            // Extra libraries provided by packages through task 'aemSatisfy'
            // or configurations: 'aemEmbed', 'aemInstall'.

            compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: '1.2.70'
            compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.2.70'
            compileOnly group: 'org.hashids', name: 'hashids', version: '1.0.1'

            // Tests runtime

            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.1'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.1'

            testImplementation group: 'io.wcm', name: 'io.wcm.testing.aem-mock.junit5', version: '2.3.2'
        }
    }

}
